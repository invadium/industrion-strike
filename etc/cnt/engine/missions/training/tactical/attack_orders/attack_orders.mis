MISSION

############################################
# Training mission "Attack Orders"
# Created by Shock (Igor Khotin) 08.06.2005
############################################

#Sides declaration variables
$Sides = 2;
$SideName1 = "Interseptors";
$SideInfo1 = "(you)";
$SideName2 = "Drones";


############################
#Creating far space objects
############################
CREATE "Proxima"
CLASS "Moon"
AT 20000000 -10000000 100000000;


###########################
#Creating navigation buoys
###########################
CREATE "Buoy A"
CLASS "Buoy"
AT	-5000	0	10000;

CREATE "Buoy B"
CLASS "Buoy"
AT	5000	0	10000;

######################################
# Interseptors side
######################################
CREATE "Interseptor 1"
	CLASS "Mirage"
	AT 2500 0 -10000;
SET "Interseptor 1" SIDE = 1;
SET "Interseptor 1" PLAYER_CONTROLLED = 0;

CREATE "Interseptor 2"
	CLASS "Mirage"
	AT 2000 0 -10000;
SET "Interseptor 2" SIDE = 1;
SET "Interseptor 2" PLAYER_CONTROLLED = 0;

######################################
# Drones side
######################################
CREATE ""
	CLASS "Drone"
	AT -24000 0 1000;
SET "Drone 1" SIDE = 2;
SET "Drone 1" PLAYER_PLACED = 0;

CREATE ""
	CLASS "Drone"
	AT -24000 0 -2000;
SET "Drone 2" SIDE = 2;
SET "Drone 2" PLAYER_PLACED = 0;

CREATE ""
	CLASS "Drone"
	AT 15000 0 1000;
SET "Drone 3" SIDE = 2;
SET "Drone 3" PLAYER_PLACED = 0;


#Left drones flight plan
PLAN 1 SIDE 2
FOLLOW "Buoy A"
WITH AGRESSION 1;

#Right drones flight plan
PLAN 2 SIDE 2
FOLLOW "Buoy B"
WITH AGRESSION 1;


BIND_CAMERA_TO "Interseptor 1";

$Mission = -1;
$KILL = 0;
$MUSTKILL = 3;

PROC NewKill;
	$KILL = $KILL + 1;
	IF ($KILL == $MUSTKILL) CALL MSuccess;
END PROC;

PROC Reached;
	IF (($ReachedObject == "Buoy A") | ($ReachedObject == "Buoy B")) CALL MFailed;
END PROC;

PROC MSuccess;
	IF ($Mission < -1) EXIT;
	$Mission = 1;
	SHOW_FINISH;
END PROC;

PROC MFailed;
	$Mission = -2;
	SHOW_FINISH;
END PROC;

PROC ON TIME 0.1;
	MESSAGE "We locate drones around you" BEEP;
END PROC;

PROC ON TIME 0.2;
	MESSAGE "Give orders to your fighters to destroy them all" BEEP;
END PROC;

PROC ON TIME 0.3;
	MESSAGE "before they reached their targets" BEEP;
	SET "Drone 1" PLAN = 1;
	SET "Drone 1" REACHPROC = "Reached";
	SET "Drone 1" KILLPROC = "NewKill";
	SET "Drone 2" PLAN = 1;
	SET "Drone 2" REACHPROC = "Reached";
	SET "Drone 2" KILLPROC = "NewKill";
	SET "Drone 3" PLAN = 2;
	SET "Drone 3" REACHPROC = "Reached";
	SET "Drone 3" KILLPROC = "NewKill";	
END PROC;